plugins {
    id 'multiloader-loader'
    id 'fabric-loom'
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${parchment_minecraft}:${parchment_version}@zip")
    }
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"

    modImplementation "maven.modrinth:modmenu:${modmenu_version}"

    implementation(group: 'io.projectreactor', name: 'reactor-core', version: '3.5.5')

    modImplementation "fuzs.forgeconfigapiport:forgeconfigapiport-fabric:21.1.0"

    modApi "dev.ultreon.mods:xinexlib-fabric-1.21.1:1.21.1-SNAPSHOT"

    include implementation('com.github.Nuix:jnode-fs:v1.0.1')
    include "org.apache.commons:commons-lang3:3.14.0" // Possibly fix reference error in 1.20.1+
}

loom {
    def aw = project(':common').file("src/main/resources/${mod_id}.accesswidener")
    if (aw.exists()) {
        accessWidenerPath.set(aw)
    }
    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }
    runs {
        client {
            client()
            setConfigName('Fabric Client')
            ideConfigGenerated(true)
            runDir('runs/client')
            vmArgs("-Dxinexlib.dev=true")
        }
        server {
            server()
            setConfigName('Fabric Server')
            ideConfigGenerated(true)
            runDir('runs/server')
            vmArgs("-Dxinexlib.dev=true")
        }
    }
}
